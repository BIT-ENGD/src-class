Genre   Name                       Year 
Comedy  Shrek Forever After        2010
Drama   The Karate Kid             2010
Action  Iron Man 2                 2010
Action  Robin Hood                 2010
Drama   The Kids Are All Right     2010
Sci-Fi  Inception                  2010

 Genre   Name                                     Year 
 Comedy  Shrek Forever After                      2010
 Drama   The Karate Kid, The Kids Are All Right   2010
 Action  Iron Man 2, Robin Hood                   2010
 Sci-Fi  Inception                                2010

Dim result = From p In movies Group p By p.Genre Into Group Select New Movie With {
         .Genre = Genre,
         .Name = String.Join(", ", Group.Select(Function(p) p.Name).ToArray()),
         .Year = Group.Select(Function(p) p.Year).FirstOrDefault
        }

Module Module1

    Sub Main()
        Dim movies As New List(Of Movie)
        movies.Add(New Movie("Comedy", "Shrek Forever After", 2010))
        movies.Add(New Movie("Drama", "The Karate Kid", 2010))
        movies.Add(New Movie("Action", "Iron Man 2", 2010))
        movies.Add(New Movie("Action", "Robin Hood", 2010))
        movies.Add(New Movie("Drama", "The Kids Are All Right", 2010))
        movies.Add(New Movie("Sci-Fi", "Inception", 2010))

        'Group amd Merge by genre
        Dim result = From p In movies Group p By p.Genre Into Group Select New Movie With {
         .Genre = Genre,
         .Name = String.Join(", ", Group.Select(Function(p) p.Name).ToArray()),
         .Year = Group.Select(Function(p) p.Year).FirstOrDefault
        }

        For Each r In result
            Console.WriteLine(r)
        Next
        Console.ReadKey()
    End Sub

    Public Class Movie
        Public Genre As String
        Public Name As String
        Public Year As Integer

        Public Sub New()
        End Sub

        Public Sub New(ByVal genre As String, ByVal name As String, ByVal year As Integer)
            Me.Year = year
            Me.Genre = genre
            Me.Name = name
        End Sub

        Public Overrides Function ToString() As String
            Return String.Format("Genre: {0}; Name:{1}; Year:{2}", Genre, Name, Year)
        End Function
    End Class

End Module

