'    WakeOnLAN - Wake On LAN
'    Copyright (C) 2004-2018 Aquila Technology, LLC. <webmaster@aquilatech.com>
'
'    This file is part of WakeOnLAN.
'
'    WakeOnLAN is free software: you can redistribute it and/or modify
'    it under the terms of the GNU General Public License as published by
'    the Free Software Foundation, either version 3 of the License, or
'    (at your option) any later version.
'
'    WakeOnLAN is distributed in the hope that it will be useful,
'    but WITHOUT ANY WARRANTY; without even the implied warranty of
'    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
'    GNU General Public License for more details.
'
'    You should have received a copy of the GNU General Public License
'    along with WakeOnLAN.  If not, see <http://www.gnu.org/licenses/>.

Imports System.Net
Imports System.Linq
Imports System.Threading
Imports Machines
Imports WOL
Imports WOL.AquilaWolLibrary
Imports Microsoft.Win32

Public Module Module1
    Enum ModeTypes
        None
        Shutdown
        WakeUp
        Abort
        Debug
        Sleep
        Hibernate
        Enumerate
        Message
        Password
    End Enum

    Enum ErrorCodes
        Ok = 0
        InvalidCommand = 1
        NotFound = 2
        PermissionDenied = 5
    End Enum

    Dim _machine As String = String.Empty
    Dim _mac As String = String.Empty
    Dim _all As Boolean = False
    Dim _agent As String = "255.255.255.255"
    Dim _alertMessage As String = "System is shutting down"
    Dim _delay As Long = 30
    Dim _force As Long = 0
    Dim _reboot As Long = 0
    Dim _group As String = String.Empty
    Dim _mode As ModeTypes = ModeTypes.None
    Dim _result As Integer = ErrorCodes.Ok
    Dim _path As String = String.Empty
    Dim _repeatInterval As Integer = 750
    Dim _password As String = String.Empty

    Function Main() As Integer
        Console.ForegroundColor = ConsoleColor.Green
        Console.WriteLine(My.Application.Info.Title & " " & My.Application.Info.Version.ToString)
        Console.ResetColor()

        If My.Application.CommandLineArgs.Count = 0 Then
            DisplayHelp()
            Return ErrorCodes.Ok
        End If

        For i As Integer = 0 To My.Application.CommandLineArgs.Count - 1
            Select Case My.Application.CommandLineArgs.Item(i)
                Case "-t"
                    If i = My.Application.CommandLineArgs.Count - 1 Then
                        BadCommand()
                        Return ErrorCodes.InvalidCommand
                    End If
                    _delay = Val(My.Application.CommandLineArgs.Item(i + 1))
                    i += 1

                Case "-f"
                    _force = 1

                Case "-m"
                    If i = My.Application.CommandLineArgs.Count - 1 Then
                        BadCommand()
                        Return ErrorCodes.InvalidCommand
                    End If
                    i += 1
                    _machine = My.Application.CommandLineArgs.Item(i)

                Case "-p"
                    If i = My.Application.CommandLineArgs.Count - 1 Then
                        BadCommand()
                        Return ErrorCodes.InvalidCommand
                    End If
                    i += 1
                    _path = My.Application.CommandLineArgs.Item(i)

                Case "-mac"
                    If i = My.Application.CommandLineArgs.Count - 1 Then
                        BadCommand()
                        Return ErrorCodes.InvalidCommand
                    End If
                    i += 1
                    _mac = My.Application.CommandLineArgs.Item(i)

                Case "-g"
                    If i = My.Application.CommandLineArgs.Count - 1 Then
                        BadCommand()
                        Return ErrorCodes.InvalidCommand
                    End If
                    i += 1
                    _group = My.Application.CommandLineArgs.Item(i)

                Case "-all"
                    _all = True

                Case "-agent"
                    If i = My.Application.CommandLineArgs.Count - 1 Then
                        BadCommand()
                        Return ErrorCodes.InvalidCommand
                    End If
                    i += 1
                    _agent = My.Application.CommandLineArgs.Item(i)

                Case "-r"
                    _mode = ModeTypes.Shutdown
                    _reboot = 1

                Case "-s"
                    _mode = ModeTypes.Shutdown
                    _reboot = 0

                Case "-s1"
                    _mode = ModeTypes.Sleep
                    _reboot = 0

                Case "-s4"
                    _mode = ModeTypes.Hibernate
                    _reboot = 0

                Case "-w"
                    _mode = ModeTypes.WakeUp

                Case "-a"
                    _mode = ModeTypes.Abort

                Case "-c"
                    If i = My.Application.CommandLineArgs.Count - 1 Then
                        BadCommand()
                        Return ErrorCodes.InvalidCommand
                    End If
                    _alertMessage = My.Application.CommandLineArgs.Item(i + 1)
                    i += 1

                Case "-pw"
                    If i = My.Application.CommandLineArgs.Count - 1 Then
                        BadCommand()
                        Return ErrorCodes.InvalidCommand
                    End If
                    _mode = ModeTypes.Password
                    _password = My.Application.CommandLineArgs.Item(i + 1)
                    i += 1

                Case "-l"
                    Listen()
                    Return ErrorCodes.Ok

                Case "-e"
                    _mode = ModeTypes.Enumerate

                Case "-msg"
                    _mode = ModeTypes.Message

                Case "-h"
                    DisplayHelp()
                    Return ErrorCodes.Ok

                Case "-d"
                    _mode = ModeTypes.Debug

                Case Else
                    DisplayHelp()
                    Return ErrorCodes.InvalidCommand

            End Select

        Next

        Select Case _mode

            Case ModeTypes.Shutdown, ModeTypes.Sleep, ModeTypes.Hibernate
                Return Shutdown()

            Case ModeTypes.WakeUp
                Return DoWakeup()

            Case ModeTypes.Abort
                Return Abort()

            Case ModeTypes.Debug
                Return ShowIPConfig()

            Case ModeTypes.Enumerate
                Enumerate()

            Case ModeTypes.Message
                SendMessage()

            Case ModeTypes.Password
                ChangePassword()

            Case Else
                BadCommand()
                Return ErrorCodes.InvalidCommand

        End Select

    End Function

    Private Sub DisplayHelp()
        Console.WriteLine()
        Console.WriteLine("Commands are:")
        Console.WriteLine("-s   (shutdown) requires -m, -g or -all")
        Console.WriteLine("-s1  (sleep) requires -m or -all")
        Console.WriteLine("-s4  (hibernate) requires -m or -all")
        Console.WriteLine("-a   (abort a shutdown) requires -m (depreciated)")
        Console.WriteLine("-r   (reboot)")
        Console.WriteLine("-w   (wakeup) requires -m, -g, -mac parameter, or -all")
        Console.WriteLine("     Use -m with -w to send to a specific subnet")
        Console.WriteLine("     Use -mac and -agent to explicitly send WOL packet to a specific IP")
        Console.WriteLine("-l   (listen for WOL packets)")
        Console.WriteLine("-e   (enumerate machine list)")
        Console.WriteLine("-p   (path to machines.xml (optional))")
        Console.WriteLine("-msg (immediate message (example -msg -c ""Shutting down in 10 minutes"")")
        Console.WriteLine("     Optional: Use -m to send message to a remote machine.")
        Console.WriteLine("-d   (debug) requires -m")
        Console.WriteLine("-pw  (password) change shutdown password for a machine, requires -m, -g or -all")
        Console.WriteLine("-h   (display this help message)")
        Console.WriteLine()
        Console.WriteLine("options:")
        Console.WriteLine("-t xx      delay (xx = seconds).  For Shutdown and Reboot commands.")
        Console.WriteLine("-f         force files closed.  For Shutdown and Reboot commands.")
        Console.WriteLine("-m xx      xx = machine name.")
        Console.WriteLine("-mac xx    xx = mac address.")
        Console.WriteLine("-g xx      xx = group name.  To wakeup or shutdown a group of machines.")
        Console.WriteLine("-all       all machines.")
        Console.WriteLine("-agent xx  xx = address of WOL agent if on remote subnet, use with -mac.")
        Console.WriteLine("-c ""xx""    xx = popup message.  Optional popup message on shutdown.")
        Console.WriteLine()
    End Sub

    Private Sub BadCommand()
        Console.ForegroundColor = ConsoleColor.Red
        Console.WriteLine("Invalid command line.")
        Console.ResetColor()
        DisplayHelp()
    End Sub

    Private Function DoWakeup() As Integer
        Dim machine As Machine

        Dim regKey As RegistryKey = Registry.CurrentUser.OpenSubKey("Software\Aquila Technology\WakeOnLAN", False)
        If (IsNothing(regKey)) Then
            _repeatInterval = 750
        Else
            _repeatInterval = regKey.GetValue("RepeatInterval", 750, RegistryValueOptions.None)
            regKey.Close()
        End If

        If _all Then
            Try
                Machines.Load(_path)

            Catch ex As Exception
                _result = ErrorCodes.PermissionDenied
                Return _result

            End Try

            For Each machine In Machines
                Console.Write("Wakeup " & machine.Name & "... ")
                If machine.MAC = "" Then
                    _result = ErrorCodes.NotFound
                    Console.WriteLine("Cannot find MAC address for " & machine.Name)
                Else
                    Console.WriteLine("waking up mac: " & machine.MAC)
                    WakeUp(machine)
                End If
            Next
            Return _result

        ElseIf _machine.Length Then
            Try
                Machines.Load(_path)

            Catch ex As Exception
                _result = ErrorCodes.PermissionDenied
                Return _result

            End Try

            machine = Machines(_machine)
            If machine Is Nothing Then
                Console.WriteLine("Cannot find machine " & _machine)
                Return ErrorCodes.NotFound
            End If
            Console.Write("wakeup sent to ")
            Console.ForegroundColor = ConsoleColor.Cyan
            Console.WriteLine(_machine)
            Console.ResetColor()
            Console.Write("waking up mac: " & machine.MAC & " broadcast: ")
            If (machine.Method = 0) Then
                Console.WriteLine(machine.Broadcast)
            Else
                Console.WriteLine(machine.Netbios)
            End If
            WakeUp(machine, _repeatInterval)
            Return ErrorCodes.Ok

        ElseIf _mac.Length Then
            Console.WriteLine("waking up mac: " & _mac)
            WakeUp(_mac, _agent)
            Return ErrorCodes.Ok

        ElseIf _group.Length Then
            Try
                Machines.Load(_path)

            Catch ex As Exception
                _result = ErrorCodes.PermissionDenied
                Return _result

            End Try

            Console.WriteLine("waking up group: " & _group)
            For Each machine In From machine1 As Machine In Machines Where machine1.Group = _group
                Console.WriteLine("  > " & machine.Name)
                WakeUp(machine, _repeatInterval)
                Thread.Sleep(500)
            Next
            Console.WriteLine("Done.")
            Return ErrorCodes.Ok

        Else
            Console.WriteLine("Error.  No machine or MAC specified.")
            DisplayHelp()
            Return ErrorCodes.InvalidCommand

        End If

    End Function

    Private Function Abort() As Integer
        Console.WriteLine("Error.  Abort command is depreciated.")
        Return ErrorCodes.InvalidCommand
    End Function

    Private Function Shutdown() As Integer
        'TODO: handle _delay
        Dim machine As Machine
        Dim flags As ShutdownFlags

        Machines.Load(_path)

        If (String.IsNullOrEmpty(_machine) And String.IsNullOrEmpty(_group) And (_all = False)) Then
            Console.WriteLine("Error.  No machine specified.")
            DisplayHelp()
            _result = ErrorCodes.InvalidCommand
            Return _result
        End If

        If _mode = ModeTypes.Shutdown Then
            If _force Then
                If _reboot Then
                    flags = ShutdownFlags.ForcedReboot
                Else
                    flags = ShutdownFlags.ForcedShutdown
                End If
            Else
                If _reboot Then
                    flags = ShutdownFlags.Reboot
                Else
                    flags = ShutdownFlags.Shutdown
                End If
            End If
        ElseIf _mode = ModeTypes.Hibernate Then
            flags = ShutdownFlags.Hibernate
        ElseIf _mode = ModeTypes.Sleep Then
            flags = ShutdownFlags.Sleep
        End If

        If _all Then
            For Each machine In Machines
                Console.WriteLine(String.Format("Command {0} to {1}", flags.ToString(), machine.Name))
                ProcessMachine(machine, flags)
            Next
        ElseIf (_group.Length) Then
            Console.WriteLine(String.Format("Command {0} to group: {1}", flags.ToString(), _group))
            For Each machine In From machine1 As Machine In Machines Where machine1.Group = _group
                Console.Write("  > " & machine.Name)
                ProcessMachine(machine, flags)
            Next
            Console.WriteLine("Done.")
        Else
            machine = Machines(_machine)
            Console.Write(flags.ToString() & " sent to " & machine.Name)
            ProcessMachine(machine, flags)
        End If

        Return _result

    End Function

    Private Sub ProcessMachine(machine As Machine, flags As AquilaWolLibrary.ShutdownFlags)
        Dim encryption As New Encryption()

        Try
            Select Case machine.ShutdownMethod
                Case Machine.ShutdownMethods.WMI
                    AquilaWolLibrary.Shutdown(machine.Netbios, flags, machine.UserID, encryption.Decrypt(machine.Password), machine.Domain)

                Case Machine.ShutdownMethods.Custom
                    Dim cmd As String = machine.ShutdownCommand
                    cmd = cmd.Replace("$PF", Environment.GetFolderPath(Environment.SpecialFolder.ProgramFiles))
                    cmd = cmd.Replace("$PFX86", Environment.GetFolderPath(Environment.SpecialFolder.ProgramFilesX86))
                    cmd = cmd.Replace("$USER", machine.UserID)
                    cmd = cmd.Replace("$PASS", encryption.Decrypt(machine.Password))
                    cmd = cmd.Replace("$HOST", machine.Netbios)
                    Debug.WriteLine("Custom command: " & cmd)
                    Shell(cmd, AppWinStyle.Hide, False)

                Case Machine.ShutdownMethods.Legacy
                    Select Case flags
                        Case ShutdownFlags.Shutdown
                            flags = ShutdownFlags.LegacyShutdown

                        Case ShutdownFlags.ForcedShutdown
                            flags = ShutdownFlags.LegacyForcedShutdown

                        Case ShutdownFlags.Reboot
                            flags = ShutdownFlags.LegacyReboot

                        Case ShutdownFlags.ForcedReboot
                            flags = ShutdownFlags.LegacyForcedReboot

                        Case Else
                            Throw New Exception(String.Format("{0} not supported with Legacy shutdown method.", flags.ToString()))

                    End Select

                    AquilaWolLibrary.Shutdown(machine.Netbios, flags, machine.UserID, encryption.Decrypt(machine.Password), machine.Domain, _alertMessage)

            End Select

            Console.WriteLine("...Successful")
            WriteLog(String.Format("Sending {0} to ""{1}""", flags.ToString(), machine.Name), EventLogEntryType.Information, EventId.Shutdown)

        Catch ex As Exception
            Console.ForegroundColor = ConsoleColor.Red
            Console.Write("...Error: ")
            Console.WriteLine(ex.Message)
            Console.ResetColor()
            WriteLog(String.Format("Shutdown error on host {0}{1}: {2}", machine.Name, vbCrLf, ex.Message), EventLogEntryType.Error, EventId.Error)

        End Try

    End Sub

    Private Sub SendMessage()
        Dim host As String = String.Empty

        Try
            If Not String.IsNullOrEmpty(_machine) Then
                If Machines.Count = 0 Then Machines.Load(_path)
                Dim machine As Machine = Machines(_machine)
                host = machine.Netbios
            End If

            Shell(String.Format("msg * /server:{0} ""{1}""", host, _alertMessage))

        Catch ex As Exception
            Console.ForegroundColor = ConsoleColor.Red
            Console.Write("...Error: ")
            Console.WriteLine(ex.Message)
            Console.ResetColor()

        End Try

    End Sub

    Private Sub Listen()
        Dim sn As New Listener

        Console.WriteLine("Listening for WakeUp packets.  CTRL-C to abort...")
        Do
            sn.BeginReceive()
        Loop

    End Sub

    Private Sub Enumerate()
        Console.WriteLine("Enumerate machines...")

        Machines.Load(_path)
        For Each m As Machine In Machines
            Console.ForegroundColor = ConsoleColor.Green
            Console.Write("{0,-16}", m.Name)
            Console.ResetColor()
            Console.WriteLine(" IP: {0,-16} MAC: {1}", m.IP, m.MAC)
        Next
    End Sub

    Private Function ChangePassword() As Integer
        Dim machine As Machine
        Dim encryption As New Encryption()
        Dim encrypted As String = encryption.Encrypt(_password)

        Machines.Load(_path)

        If (String.IsNullOrEmpty(_machine) And String.IsNullOrEmpty(_group) And (_all = False)) Then
            Console.WriteLine("Error.  No machine specified.")
            DisplayHelp()
            _result = ErrorCodes.InvalidCommand
            Return _result
        End If

        If _all Then
            Console.WriteLine(String.Format("Changing password all machines"))
            For Each machine In Machines
                Console.WriteLine("  > " & machine.Name)
                machine.Password = encrypted
            Next
        ElseIf (_group.Length) Then
            Console.WriteLine(String.Format("Changing password for group: {0}", _group))
            For Each Machine In From machine1 As Machine In Machines Where machine1.Group = _group
                Console.WriteLine("  > " & machine.Name)
                machine.Password = encrypted
            Next
        Else
            Machine = Machines(_machine)
            Console.WriteLine("Changing password: {0}", machine.Name)
            machine.Password = encrypted
        End If

        Machines.Save(_path)

        Console.WriteLine("Done.")
        Return _result

    End Function

    Private Function ShowIPConfig() As Integer
        Dim ping As New NetworkInformation.Ping
        Dim reply As NetworkInformation.PingReply

        If (_machine = "") Then
            Console.WriteLine("You must specify machine name with -m")
            Return ErrorCodes.InvalidCommand
        End If
        Console.WriteLine("Show IP configuration for " & _machine)
        Try
            For Each IPA As IPAddress In Dns.GetHostAddresses(_machine)
                Console.WriteLine("  Address: {0} Family: {1}", IPA.ToString(), IPA.AddressFamily.ToString())
            Next

        Catch ex As Exception
            Console.WriteLine(ex.Message)
            Return ErrorCodes.NotFound

        End Try

        reply = ping.Send(_machine, 1500)
        Console.WriteLine("Ping: " & reply.Status.ToString())
        Return ErrorCodes.Ok

    End Function

End Module
